apply plugin: 'groovy'
// http://central.sonatype.org/pages/gradle.html
apply plugin: 'maven'
apply plugin: 'signing'

group = 'info.hubbitus'
version = '1.2'

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.codehaus.groovy:groovy-all:2.4.7'

	compile 'org.slf4j:slf4j-api:1.7.12'
	compile 'ch.qos.logback:logback-classic:1.1.3' // dual EPL v.1.0 and LGPL 2.1

	// mandatory dependencies for using Spock
	compile 'org.codehaus.groovy:groovy-all:2.4.7'
	testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
	testCompile 'org.codehaus.gpars:gpars:+'
	testCompile 'org.hamcrest:hamcrest-library:+' // JUnit regexp checks. See http://matchers.jcabi.com/regex-matchers.html
	testCompile 'com.jcabi:jcabi-matchers:+' // JUnit regexp checks. See http://matchers.jcabi.com/regex-matchers.html
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.1'
}

///////////////////////////////////////////////////////////////
// Artifacts publishing into central OSS repository
// http://central.sonatype.org/pages/gradle.html
task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}
signing {
//    required { gradle.taskGraph.hasTask("uploadArchives") } // https://groups.google.com/forum/#!topic/gaelyk/WfdEDBOzIOM
	sign configurations.archives
}


uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name project.name
				packaging 'jar'
				description 'Collection of tiny but very helpful utility classes like ProgressLogger, ConfigExtended and some benchmark classes. Java and Groovy.'
				url 'https://github.com/Hubbitus/Hubbitus-common'

				scm {
					connection 'scm:git:git@github.com:Hubbitus/Hubbitus-common.git'
					developerConnection 'scm:git:git@github.com:Hubbitus/Hubbitus-common.git'
					url 'https://github.com/Hubbitus/Hubbitus-common'
				}

				licenses {
					license {
						name 'The Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id 'Hubbitus'
						name 'Pavel Alexeev'
						email 'Pahan@Hubbitus.info'
					}
				}
			}
		}
	}
}
// /Artifacts publishing into central OSS repository
////////////////////////////////////////////////////
